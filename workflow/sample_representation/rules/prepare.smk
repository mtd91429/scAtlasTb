use rule pseudobulk from sample_representation as prepare with:
    input:
        zarr=lambda wildcards: mcfg.get_input_file(**wildcards)
    output:
        zarr=directory(mcfg.out_dir / 'prepare' / 'dataset~{dataset}' / 'file_id~{file_id}' / 'prepare.zarr'),
        bulks=directory(mcfg.out_dir / 'prepare' / 'dataset~{dataset}' / 'file_id~{file_id}' / 'bulks.zarr'),
    params:
        sample_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'sample_key'),
        cell_type_key=lambda wildcards: mcfg.get_from_parameters(wildcards, 'cell_type_key'),
        layer=lambda wildcards: mcfg.get_from_parameters(wildcards, 'norm_counts', default='X'),
        min_cells_per_sample=lambda wildcards: mcfg.get_from_parameters(wildcards, 'min_cells_per_sample', default=1),
        min_cells_per_cell_type=lambda wildcards: mcfg.get_from_parameters(wildcards, 'min_cells_per_cell_type', default=1),
        aggregate=lambda wildcards: mcfg.get_from_parameters(wildcards, 'aggregate', default='sum'),
        dask=lambda wildcards: mcfg.get_from_parameters(wildcards, 'dask', default=None),
    conda:
        get_env(config, 'scanpy')
    resources:
        partition=mcfg.get_resource(resource_key='partition'),
        qos=mcfg.get_resource(profile='cpu',resource_key='qos'),
        mem_mb=lambda w, attempt: mcfg.get_resource(profile='cpu',resource_key='mem_mb', attempt=attempt),
        gpu=mcfg.get_resource(profile='cpu',resource_key='gpu'),


rule prepare_all:
    input:
        mcfg.get_output_files(rules.prepare.output),
    localrule: True
